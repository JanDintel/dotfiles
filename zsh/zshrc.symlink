# ZSH configuration

# Original dotfiles
export ZSH=$HOME/.oh-my-zsh

# All dotfiles
export DOTFILES=$HOME/.dotfiles

# Projects directory
export PROJECTS=~/Workspace

# All ZSH dotfiles
typeset -U configuration_files
configuration_files=($DOTFILES/**/*.zsh)

# Load each PATH ZSH dotfile
for configuration_file in ${(M)configuration_files:#*/path.zsh}
do
  source $configuration_file
done

# Load each ZSH dotfile, except for the PATH and completion ZSH dotfiles
for configuration_file in ${${configuration_files:#*/path.zsh}:#*/completion.zsh}
do
  source $configuration_file
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# Load each completion ZSH dotfile
for configuration_file in ${(M)configuration_files:#*/completion.zsh}
do
  source $configuration_file
done

unset configuration_files

# Load original dotfiles (must be loaded on end of file)
source $ZSH/oh-my-zsh.sh

# Load kubectl autocomplete
# source <(kubectl completion zsh)
